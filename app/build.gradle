/*
 * Copyright (C) 2018  José Miguel García Urrutia <josemgu91@gmail.com>
 *
 * This file is part of HabitTune.
 *
 * HabitTune is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * HabitTune is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    namespace 'com.josemgu91.habittune'
    compileSdk 34

    signingConfigs {
        testRelease {
            keyAlias 'test-release'
            keyPassword '123456'
            storeFile file('../test-release-keystore.jks')
            storePassword '123456'
        }
    }

    defaultConfig {
        applicationId "com.josemgu91.habittune"
        minSdk 24
        targetSdk 34
        versionCode 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions = ["data"]
    productFlavors {
        clean {
            dimension "data"
            versionName "rc-1.0-vc${defaultConfig.versionCode}"
        }
        withTestData {
            dimension "data"
            versionName "rc-1.0-vc${defaultConfig.versionCode}-internal"
        }
    }
    buildFeatures {
        buildConfig true
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_IN_RAM_DATABASE", "false"
            signingConfig signingConfigs.testRelease
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_IN_RAM_DATABASE", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def appCompatVersion = '1.7.0'
    def materialVersion = '1.12.0'
    def constraintLayoutVersion = '2.1.4'
    def recyclerViewVersion = '1.3.2'
    def cardViewVersion = '1.0.0'
    def roomVersion = "2.6.1"
    def lifecycleVersion = "2.8.3"
    def preferenceVersion = "1.0.0"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.cardview:cardview:$cardViewVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    //implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.legacy:legacy-preference-v14:$preferenceVersion"
    def sqLiteSupportLibraryVersion = '3.45.0'
    implementation "com.github.requery:sqlite-android:$sqLiteSupportLibraryVersion"
    def simpleStackVersion = '2.9.0'
    implementation "com.github.Zhuinden:simple-stack:$simpleStackVersion"
    def colorPickerVersion = '1.1.0'
    implementation "com.jaredrummler:colorpicker:$colorPickerVersion"
    def flexibleAdapterVersion = '5.1.1'
    implementation "com.github.ddB0515.FlexibleAdapter:flexible-adapter:$flexibleAdapterVersion"
    def mpAndroidChartVersion = 'v3.1.0'
    implementation "com.github.PhilJay:MPAndroidChart:$mpAndroidChartVersion"
    def fastCsvVersion = '1.0.3'
    implementation "de.siegmar:fastcsv:$fastCsvVersion"
    def appIntroVersion = '6.3.1'
    implementation "com.github.AppIntro:AppIntro:$appIntroVersion"
    def jUnitVersion = '4.13.2'
    def testRunnerVersion = '1.6.1'
    def coreTestingVersion = '2.2.0'
    def espressoVersion = '3.6.1'
    testImplementation "junit:junit:$jUnitVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"
    testImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
}

task installRelease(dependsOn: "installCleanRelease", type: Exec, group: "help") {
    commandLine 'adb', 'shell', 'am', 'start', '-n', "com.josemgu91.habittune/.android.ActivityMain"
}
